	.TITLE	SCAN
	.MCALL	.PRINT,.EXIT
	.INCLU	/SY:P16MAC/

START:	.PRINT	#VER
	MFHLT	#V.KBD
	MOV	R0,SAVKBD
	MFHLT	#V.KBD+2
	MOV	R0,SAVKBD+2
	MOV	SAVKBD,R1
	ADD	#P.PRI,R1
	MFHLT	R1
	MOV	R0,PRIKBD
	MOV	SAVKBD,R0
	HIMPRI	#-1

	CLR	CNT
	NEWROM	#KBDPRC
	MOV	R0,KBDDSC

	MOV	#PDPTR,R2
	CLR	R1
1$:	MFHLT	R2
	INC	R1
	MOV	R0,R2
	BEQ	2$
	ADD	#P.DSUCC,R2
	CMP	R0,KBDDSC
	BNE	1$
	BR	3$
2$:	CLR	R1
3$:	MOV	R1,KBDNUM

10$:	TST	CNT
	BEQ	10$
	MOVB	@RKBUF,R1
	BIC	#177400,R1
	CLR	R0
	DIV	#8.,R0
	ADD	#'0,R1
	MOVB	R1,KSTR+2
	MOV	R0,R1
	CLR	R0
	DIV	#8.,R0
	ADD	#'0,R0
	ADD	#'0,R1
	MOVB	R1,KSTR+1
	MOVB	R0,KSTR
	.PRINT	#KSTR
	CMPB	@RKBUF,#204
	BEQ	5$
	INC	RKBUF
	CMP	RKBUF,#KBDBFF+128.
	BLO	4$
	MOV	#KBDBFF,RKBUF
4$:	DEC	CNT
	BR	10$

5$:	MTPS	#200
	CLR	R0
	MTHLT	#157700
	KILPRO	KBDNUM
	MOV	SAVKBD+2,R0
	MTHLT	#V.KBD+2
	MOV	SAVKBD,R0
	MTHLT	#V.KBD
	BIC	#M.KBD,@#PICMR
	MOV	SAVKBD,R0
	HIMPRI	PRIKBD
	.EXIT

KBDPRC:	MOV	#700, SP
	SETPRI	#50000
	UNPVEC	#V.KBD
	CLR	R0
	PROVEC	#V.KBD
1$:	WAITINT	#M.KBD
	MOV	#120,@#KBDCSR
	MOV	#MTX1,R0
	MOV	#MTX2,R1
	MOV	#8.,R5
2$:	MOV	@#KBDBUF,R4
	MOV	@R0,@R1
	MOV	R4,R3
	BIC	#7,R3
	ASL	R3
	ASL	R3
	ASL	R3
	BIC	#^C7,R4
	BISB	MTXTBL(R4),R3
	XOR	R3,(R1)+
	MOV	R3,(R0)+
	SOB	R5,2$
	MOV	#302,@#KBDCSR
	MOV	#8.,R5
3$:	MOV	#8.,R0
	SUB	R5,R0
	ASL	R0
	MOV	MTX2(R0),R1
	BEQ	7$
	MOV	#1,R2
	MOV	#1,R3
4$:	BIT	R2,R1
	BEQ	6$
	MOV	R0,R4
	ASL	R4
	ASL	R4
	ASL	R4
	BISB	MTXTB2(R3),R4
	CMPB	R4,#45
	BNE	8$
	MOV	#4,R4
8$:	BIT	R2,MTX1(R0)
	BNE	5$
	BIS	#200,R4
5$:	MOVB	R4,@WKBUF
	INC	WKBUF
	INC	CNT
	CMP	WKBUF,#KBDBFF+128.
	BLO	6$
	MOV	#KBDBFF,WKBUF
6$:	ASL	R2
	BMI	7$
	INC	R3
	BR	4$
7$:	SOB	R5,3$
	BR	1$


CNT:	.WORD	0
RKBUF:	.WORD	KBDBFF
WKBUF:	.WORD	KBDBFF
KBDBFF:	.BLKB	128.
MTX1:	.WORD	0,0,0,0,0,0,0,0
MTX2:	.WORD	0,0,0,0,0,0,0,0
MTXTBL:	.BYTE	0,1,2,4,10,20,40,0
MTXTB2:	.BYTE	0,7,10,12,11,15,17,13,14,16,5,6,0,0,0,0
SAVKBD:	.BLKW	2
PRIKBD:	.BLKW	1
KBDDSC:	.BLKW	1
KBDNUM:	.BLKW	1
VER:	.ASCIZ	<33>/</<33>/[?7hScan-code of keys, "Alphaprog", 2023/
KSTR:	.ASCII	/000 /<200>
	.END START
